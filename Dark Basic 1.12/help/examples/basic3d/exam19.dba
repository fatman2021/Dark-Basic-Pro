Rem * Title  : Static Objects
Rem * Author : DBS-LB
Rem * Date   : 8th September 2000
rem ==========================================
rem DARK BASIC EXAMPLE PROGRAM 19
rem ==========================================
rem This program shows use of static objects
rem ------------------------------------------
autocam off : sync rate 0
hide mouse : sync on

rem Set camera
turn camera right 45
move camera 50

rem Different types of static object
type=0
do

rem Delete static objects (from any last cycle)
delete static objects

rem Make a new scene of objects
objid=1
for z=0 to 9
	for x=0 to 9

		rem Make and position an object
		if x/2.0=int(x/2.0) or z/2.0=int(z/2.0)
			make object plain objid,100,100
			largeobject=0
		else
			make object sphere objid,100
			largeobject=1
		endif
		xrotate object objid,90
		position object objid,x*100,-10,z*100
		color object objid,rgb(rnd(255),rnd(255),rnd(255))

		rem If a static type, create now from original object
		if type>0
			if type=1 then make static object objid
			if type=2
				if largeobject=1
					make static object objid,1
				else
					make static object objid,0
				endif
			endif

			rem Delete the original object
			delete object objid

		endif

		rem Increment object counter
		inc objid

	next x
next z

rem Begin main loop
do

rem User prompt
set cursor 0,0
print "Polygons:";statistic(1);"  Framerate:";screen fps()
if type=0 then print "Normal Objects"
if type=1 then print "Standard Static Objects"
if type=2 then print "Occluded Static Objects"
print "Hit Return to Change"

rem Move camera around
if leftkey()=1 then turn camera left 4
if rightkey()=1 then turn camera right 4
if upkey()=1 then move camera 2
if downkey()=1 then move camera -2

rem Try another type
if returnkey()=1 then exit

rem Update screen
sync

rem End of loop
loop

rem Delete objects (if any)
for t=1 to 1000
	if object exist(t)=1 then delete object t
next t

rem Next tpe
inc type : if type=3 then type=0

rem Type loop
loop

