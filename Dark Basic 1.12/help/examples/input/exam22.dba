Rem * Title  : Advanced Joystick
Rem * Author : DBS-LB
Rem * Date   : June 2001
rem ===============================================
rem DARK BASIC EXAMPLE PROGRAM 22
rem ===============================================
rem This program controls an advanced joystick
rem -----------------------------------------------

rem Set a coordinate in the middle of the screen
x=320
y=240

Rem Activate manual sync
sync on

rem Begin main loop
do

rem Clear screen
cls

rem Show the analogue values
set cursor 0,10
print "MovementX=";joystick x()
print "MovementY=";joystick y()
print "MovementZ=";joystick z()
print "SliderA=";joystick slider a()
print "SliderB=";joystick slider b()
print "SliderC=";joystick slider c()
print "SliderD=";joystick slider d()
print "Twist x=";joystick twist x()
print "Twist y=";joystick twist y()
print "Twist z=";joystick twist z()
for t=0 to 3
	print "Hat";t;"=";joystick hat angle(t)
next t

rem Show all fire buttons as flags
print "FIRE BUTTON FLAGS: ";
for t=0 to 31
	print joystick fire x(t);" ";
next t
print

rem Move the coordinate based on the joystick POV controller (hat)
x=x+(joystick x()/100)
y=y+(joystick y()/100)

rem Make sure the coordinate does not leave the screen
if x<0 then x=0
if x>640 then x=640
if y<0 then y=0
if y>480 then y=480

rem Draw a circle at the coordinate
for dy=-2 to 2
	for dx=-2 to 2
		for t=0 to 2
			circle x+dx,y+dy,40+t
		next t
	next dx
next dy

rem Draw hat direction
hatangle=wrapvalue(joystick hat angle(0)/100)
line x,y,newxvalue(x,hatangle,50),newzvalue(y,hatangle,-50)

Rem Syncronise drawing
sync

rem End the loop
loop

