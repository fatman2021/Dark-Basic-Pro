Rem * Title  : Sending Images
Rem * Author : DBS-LB
Rem * Date   : October 2001
rem ===============================================
rem DARK BASIC EXAMPLE PROGRAM 04
rem ===============================================
rem This program handles Sending Images
rem -----------------------------------------------

rem Make Net Game
gosub _join_or_host

rem Nice font
set text font "arial"
set text size 18
set text opaque
set text to bold

rem Main game loop
cls rgb(255,255,255)
if iam$="host" then rcol=rgb(255,255,0)
if iam$="joiner" then rcol=rgb(0,128,255)
do
	
	rem Label text
	ink rgb(0,0,0),rgb(255,255,255)
	center text 160,8,"MULTIPLAYER IMAGE EXCHANGE"
	text 16,32+10,"Source:"

	rem Make an Image
	imgnum=100
	ink 0,rgb(255,255,255)
	if rnd(200)=0 then box 100,32,131,32+31
	ink rcol,0
	dot 100+rnd(31),32+rnd(31)
	get image imgnum,100,32,132,32+32
	ink rgb(255,255,255),0
	set text size 14
	set text to normal
	center text 116,32+8,str$(num)
	set text to bold
	set text size 18
	inc num

	rem Send image to all net players
	send net message image 0,imgnum,1

	rem Receive any image from other players
	repeat
		if net message type()=5
			net message image 1 : pn=net message player from()
			if image exist(1)=1
				ink rgb(0,0,0),rgb(255,255,255)
				text 16,32+(pn*32)+10,"From "+str$(pn)+":"
				paste image 1,100,32+(pn*32)
			endif
		endif
		get net message
	until net message exists()=0

	rem Update screen
	sync

loop

rem Join or Host Subroutine
_join_or_host:

	rem Set sync, display mode and an always active window
	sync on : set display mode 320,240,16 : always active on
	set window on : set window title "JOINER"

	rem Join First, Else Host (for best results run as EXECUTABLES)
	center text 160,100,"...ATTEMPTING TO JOIN..."
	center text 160,120,"[IN DB...HIT ESCAPE TO BYPASS WARNING]" : sync : sync
	join net game 1,"Bill"
	if net game exists()=0
		cls 0 : center text 160,110,"...HOSTING NEW NET GAME..." : sync : sync
		create net game "mygamename","Fred",4,0
		set window position 330,23 : set window title "HOST"
		iam$="host"
	else
		iam$="joiner"
	endif

return

